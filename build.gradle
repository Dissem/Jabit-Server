buildscript {
    ext {
        springBootVersion = '1.3.0.M5'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

configurations {
    providedRuntime
}

ext.jabitVersion = '1.0.0'
dependencies {
    compile("org.springframework.boot:spring-boot-starter-hateoas")
    compile("org.springframework.boot:spring-boot-starter-jersey")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("io.springfox:springfox-swagger2:2.0.2")
    compile("io.springfox:springfox-swagger-ui:2.0.2")

    compile "ch.dissem.jabit:jabit-core:$jabitVersion"
    compile "ch.dissem.jabit:jabit-networking:$jabitVersion"
    compile "ch.dissem.jabit:jabit-repositories:$jabitVersion"
    compile "ch.dissem.jabit:jabit-cryptography-bouncy:$jabitVersion"
    compile "ch.dissem.jabit:jabit-extensions:$jabitVersion"

    compile 'com.h2database:h2:1.4.187'

    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    testCompile("org.springframework.boot:spring-boot-starter-test")
}


eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task(copyWebapp, type: Copy, dependsOn: ":webapp:build") {
    from(file('webapp/dist'))
    into(file(buildDir.canonicalPath + '/resources/main/static'))
}

build.dependsOn copyWebapp

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
